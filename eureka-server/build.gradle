apply plugin: "application"
apply plugin: "spring-boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "docker"

springBoot {
  requiresUnpack = ["com.netflix.eureka:eureka-core", "com.netflix.eureka:eureka-client"]
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-starter-parent:${springCloudStarterVersion}"
  }
}

ext {
  commonVersion = "1.0-SNAPSHOT"
}

dependencies {
  compile("tv.acfun.cloud.common:common:${commonVersion}")

  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: "spring-boot-starter-tomcat"
  }
  compile("org.springframework.boot:spring-boot-starter-undertow")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  testCompile("org.springframework.boot:spring-boot-starter-test")

  compile("org.springframework.cloud:spring-cloud-starter-eureka-server")
  compile("org.springframework.cloud:spring-cloud-starter-config")
  compile("org.springframework.cloud:spring-cloud-starter-bus-amqp")
}

install.enabled = false
uploadArchives.enabled = false

task copyStartScript(type: Copy) {
  from {
    zipTree(configurations.compile.fileCollection { dep -> dep.group == "tv.acfun.cloud.common" && dep.name == "common" }.singleFile)
  }
  into "$buildDir/tmp/scripts"
}

startScripts {
  dependsOn copyStartScript

  unixStartScriptGenerator.template = resources.text.fromFile("$buildDir/tmp/scripts/customUnixStartScript.txt")
  doLast {
    delete windowsScript
  }
}

task buildDocker(type: Docker, dependsOn: build) {
  tagVersion = project.version + ".1"
  maintainer = "Dreampie <Dreampie@outlook.com>"
  push = true
  applicationName = jar.baseName
  dockerfile = file("src/main/docker/Dockerfile")
  registry = gradleDockerRegistry
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}